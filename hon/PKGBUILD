# Contributor:	Slash		<yahoo.com: demodevil5>
# Maintainer:	Jesse Jaara	<gmail.com: jesse.jaara>

pkgname=hon
pkgver=3.1.1
pkgrel=1
pkgdesc="Heroes of Newerth is a Real Time Strategy game heavily influcenced by DotA"
arch=('i686' 'x86_64')
url="http://www.heroesofnewerth.com/"
license=('custom:HoN')
depends=('speex' 'curl' 'alsa-lib' 'gconf' 'gtk2'  'nss' 'libxss' 'libjpeg6-turbo')
makedepends=('unzip')
options=(!strip)
install=hon.install
source=('hon.desktop' "http://dl.heroesofnewerth.com/HoNClient-${pkgver}.sh")
noextract=("HoNClient-${pkgver}.sh")

prepare() {
	unzip ${srcdir}/HoNClient-${pkgver}.sh data/*
}

package() {
    ## Thx to messo8080 for reminding uss about the /tmp dir
    ## On new installs this dir is in ram by default ^^.

    if `pwd | grep -q "/tmp"`; then
	echo \
"You are trying to build this pkg in /tmp dir.
This is not so good idea as /tmp dir is in ram
by default. If you know that you have lots of
ram (>4G) and swap and want to continue building in
the current directory write yes and press ENTER.
Otherwise write no and press ENTER.
Continue?"
	read answer
		if [ "${answer}" == "no" ]; then
			false
		fi
    fi

    # Create Destination Directory
    mkdir -p "${pkgdir}/opt/"

    # Move files
    cp -R "${srcdir}/data" "${pkgdir}/opt/hon"

    # Add link to launcher
    ln -s /opt/hon/hon.sh "${pkgdir}/usr/hon"

    # Install Desktop Shortcut
    install -Dm644 "${srcdir}/hon.desktop" "${pkgdir}/usr/share/applications/hon.desktop"

	# Install license (The actual license file is empty, but the tos.txt file is actually a license)
    install -Dm644 "${srcdir}/data/tos.txt" "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"

    ## Remove un-needed libs
    ## fmodex and linpng aren't compitable with Arch
    ## freetype causes segfault on some systems O_o
    _system_libs="libcurl.so.4  libgcc_s.so.1  libspeexdsp.so.1  libspeex.so.1  libstdc++.so.6 libfreetype.so.6 libjpeg.so.62"
    if [ "${CARCH}" == 'x86_64' ]; then
	    ## remove 32bit's
	    rm -rf "${pkgdir}"/opt/hon/*-x86{,.so}
	    rm -rf "${pkgdir}"/opt/hon/{editor,game,Awesomium}/*-x86{,.so}
	    cd "${pkgdir}/opt/hon/libs-x86_64" && rm ${_system_libs}
    else
        ## remove 64bit's
        rm -rf "${pkgdir}"/opt/hon/*-x86_64{,.so}
        rm -rf "${pkgdir}"/opt/hon/{editor,game,Awesomium}/*-x86_64{,.so}
	    cd "${pkgdir}/opt/hon/libs-x86" && rm ${_system_libs}
    fi
}

md5sums=('d3fa56f17b5eb21d4da01e8d628fbc97'
         'bf4c94b847571c2efe5199263c5dc590')

# Maintainer: Piotr Rogoza <piotr.r.public at gmail dot com>
# Contributor: Piotr Rogoa <piotr.r.public at gmail dot com>
# Contributor: Kevin Ott <supercodingmonkey@gmail.com>

pkgname=sqlitestudio
_pkgname=SQLiteStudio
pkgver=3.0.2
_pkgver=3
pkgrel=2
pkgdesc='Database manager for SQLite'
arch=('i686' 'x86_64')
url='http://sqlitestudio.pl/'
license=('GPL3')
depends=('termcap' 'tcl' 'qt5-script')
optdepends=('sqlite2')
makedepends=('chrpath')
install='sqlitestudio.install'
source=(http://sqlitestudio.pl/files/sqlitestudio$_pkgver/complete/tar/sqlitestudio-$pkgver.tar.gz
        sqlitestudio.png
        sqlitestudio.desktop
        tclconfig.diff)
sha256sums=('fbbad8c0fc1c9df4e83f9b00f94385daf94149febf97ca264250e580c5cfbe95'
            'd656ebc7fa035665cc43315e3398f7f7eae938e080c6b712ace411bb71245c1b'
            '330e1d71e6ebd858af0380c5224a7d8264546f036570e5062b68fad6ae0adf9c'
            'f63b112d42bc670ab95a264ee1d82acdefad34733c18554b17801fa5c2f56bae')

prepare() {
  cd "$srcdir"
  patch -p1 -i tclconfig.diff
}

build() {
  cd "$srcdir"

  mkdir -p "$srcdir/output/build"
  cd "$srcdir/output/build"
  qmake "../../${_pkgname}${_pkgver}"
  make

  mkdir -p "$srcdir/output/build/Plugins"
  cd "$srcdir/output/build/Plugins"
  qmake "../../../Plugins"
  make
}

package() {
  cd "$srcdir/output/build"
  make INSTALL_ROOT="$pkgdir" install

  cd "$srcdir/output/build/Plugins"
  make INSTALL_ROOT="$pkgdir" install

  install -Dm644 "$srcdir/${_pkgname}${_pkgver}/guiSQLiteStudio/img/sqlitestudio.svg" \
    "$pkgdir/usr/share/icons/hicolor/scalable/apps/sqlitestudio.svg"

  install -Dm644 "$srcdir/sqlitestudio.png" \
    "$pkgdir/usr/share/icons/hicolor/48x48/apps/sqlitestudio.png"

  install -Dm644 "$srcdir/sqlitestudio.desktop" \
    "$pkgdir/usr/share/applications/sqlitestudio.desktop"

  # Insecure RPATH
  chrpath -d "$pkgdir/usr/bin/sqlitestudio"
  chrpath -d "$pkgdir/usr/bin/sqlitestudiocli"
}
